plugins {
	id 'idea'
	id 'java-library'
	id 'maven-publish'
	id 'net.neoforged.moddev.legacyforge' version '2.0.99'
	id 'com.diffplug.spotless' version '7.0.4'
	id 'io.freefair.lombok' version '8.14'
}

tasks.named('wrapper', Wrapper).configure {
	distributionType = Wrapper.DistributionType.BIN
}

version = project.mod_version + (project.mod_state == "RELEASE" ? "" : "-" + project.mod_state) + (project.mod_state == "SNAPSHOT" ? "-" + new Date().format("yyyyMMdd.HHmmSS", TimeZone.getTimeZone("UTC")) : "")
group = project.mod_group_id
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets.main.resources { srcDir 'src/generated/resources' }

apply from: "$rootDir/gradle/scripts/moddevgradle.gradle"
apply from: "$rootDir/gradle/scripts/repositories.gradle"
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/gradle/scripts/resources.gradle"
apply from: "$rootDir/gradle/scripts/jars.gradle"
apply from: "$rootDir/gradle/scripts/spotless.gradle"

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}
tasks.withType(Copy).configureEach {
	duplicatesStrategy = 'exclude'
}
tasks.withType(Jar).configureEach {
	duplicatesStrategy = 'exclude'
}

lombok {
	version = "1.18.38"
}
configurations {
	runtimeClasspath.extendsFrom localRuntime
}
jar {
	manifest.attributes([
			"MixinConfigs": "${mod_id}.mixins.json"
	])
}
publishing {
	publications {
		register('mavenJava', MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			url "file://${project.projectDir}/repo"
		}
	}
}
idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}
